import java.text.SimpleDateFormat

// enable intellij integration & load vmf version to be used
buildscript {

    ext.commonProps = new Properties()
    file(new File(rootProject.getRootDir(),"config/common.properties")).
            withInputStream { {commonProps.load(it) }}

    ext.vmfPluginIntelliJIntegration = true

    repositories {
        mavenCentral()
    }

    dependencies {
        classpath "eu.mihosoft.vmf:vmf-gradle-plugin:${project.commonProps.get('vmf.version')}"
    }
}

plugins {
    id 'java'
    id 'com.github.hierynomus.license' version '0.16.1'
    id 'maven-publish'
    id 'net.nemerosa.versioning' version '2.7.1'
    id 'com.github.ben-manes.versions' version '0.13.0'
}

// load plugins
apply plugin: 'eu.mihosoft.vmf'
apply from: 'gradle/publishing.gradle'

// set dependencies for intellij editor (otherwise vmf dir would be ignored)
idea {
    module {
        dependencies {
            implementation group: 'eu.mihosoft.vmf', name: 'vmf', version: "${project.commonProps.get('vmf.version')}"
        }
    }
}

// configure java build
sourceCompatibility = '17'
targetCompatibility = '17'
[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

tasks.withType(JavaCompile) {
    options.compilerArgs << "-Xlint:unchecked"   //<< "-Werror"
    options.compilerArgs << "-Xlint:deprecation" //<< "-Werror"
}

tasks.withType(Javadoc) {
    options.addStringOption('Xdoclint:none', '-quiet')
    options.encoding = 'UTF-8'

    javadocTool = javaToolchains.javadocToolFor {
        languageVersion = JavaLanguageVersion.of(18)
    }
}

wrapper {
    gradleVersion '7.5.1'
}

repositories {
    mavenCentral()
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.9.0'
}

jar {
    manifest {
        attributes('Automatic-Module-Name': 'eu.mihosoft.asyncutils')
    }
}

test {
    useJUnitPlatform()
    testLogging.showStandardStreams = true
    testLogging.exceptionFormat     = 'full'
}


license {
    header = rootProject.file('config/HEADER')
    strictCheck = true
    ignoreFailures = true

    mapping {
        java   = 'SLASHSTAR_STYLE'
        groovy = 'SLASHSTAR_STYLE'
        fxml   = 'XML_STYLE'
    }

    ext.yearCurrent  = new SimpleDateFormat("yyyy").format(new Date())
    ext.yearSince1   = '2022'
    ext.yearSinceYearCurrent1 = (ext.yearCurrent == ext.yearSince1)?ext.yearSince1:ext.yearSince1 + "-" + ext.yearCurrent
    ext.author1 = 'Michael Hoffer <info@michaelhoffer.de>'

    exclude '**/*.svg'
}